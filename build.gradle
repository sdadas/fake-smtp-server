plugins {
	id 'pl.allegro.tech.build.axion-release' version '1.12.1'
	id 'org.springframework.boot' version '2.4.1'
	id "org.sonarqube" version "3.0"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

scmVersion {
	tag {
		prefix = ''
	}
}

group = 'de.gessnerfl'
description = """Fake SMTP Server :: Simple SMTP Server which stores emails in memory and lists email in a web ui"""
project.version = scmVersion.version

ext.scmUrl = 'git@github.com:gessnerfl/fake-smtp-server.git'
ext.projectUrl = 'https://github.com/gessnerfl/fake-smtp-server'

sourceCompatibility = 11
targetCompatibility = 11

repositories { jcenter() }

dependencies {
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
	implementation('org.springframework.boot:spring-boot-starter-data-jpa')
	implementation('org.springframework.boot:spring-boot-starter-mail')
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation('commons-io:commons-io:2.8.0')
	implementation('org.subethamail:subethasmtp:3.1.7')
	implementation('org.flywaydb:flyway-core')
	implementation('com.sun.xml.bind:jaxb-impl:2.3.3')
	implementation('org.springdoc:springdoc-openapi-ui:1.5.2')

	implementation('org.webjars.bower:jquery:3.5.1')
	implementation('org.webjars:materializecss:1.0.0')
	implementation('org.webjars:material-design-icons:3.0.1')

	runtimeOnly('com.h2database:h2')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.ow2.asm:asm:9.0')
	testImplementation('org.apache.commons:commons-lang3:3.11')
	testImplementation('org.hamcrest:hamcrest-core:2.2')
}

java {
	withSourcesJar()
	withJavadocJar()
}

springBoot {
	buildInfo()
}

sonarqube {
	properties {
		property 'sonar.host.url', 'https://sonarcloud.io'
		property 'sonar.organization', 'gessnerfl-github'
		property "sonar.projectName", "Fake SMTP Server"
		property "sonar.projectKey", "de.gessnerfl.fake-smtp-server"
	}
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.enabled true
	}
}
